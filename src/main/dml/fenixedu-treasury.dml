package org.fenixedu.treasury.domain;

enum org.fenixedu.treasury.domain.document.FinantialDocumentTypeEnum                                     as FinantialDocumentTypeEnum;
enum org.fenixedu.treasury.domain.document.FinantialEntryType                                            as FinantialEntryType;
enum org.fenixedu.treasury.domain.document.FinantialDocumentStateType                                    as FinantialDocumentStateType;
enum org.fenixedu.treasury.domain.tariff.DueDateCalculationType                                          as DueDateCalculationType;
enum org.fenixedu.treasury.domain.tariff.InterestType                                                    as InterestType; 
enum org.fenixedu.treasury.domain.paymentcodes.PaymentReferenceCodeStateType							 as PaymentReferenceCodeStateType;

external class .org.fenixedu.bennu.io.domain.GenericFile;

valueType java.math.BigDecimal as BigDecimalT
{
	externalizeWith {
		String toString();
	}
} 

valueType pt.ist.standards.geographic.Municipality as Municipality {
    externalizeWith {
        String pt.ist.standards.geographic.GeographicInfoLoader.externalizePlace();
    }

    internalizeWith pt.ist.standards.geographic.GeographicInfoLoader.internalizePlace();
}

valueType pt.ist.standards.geographic.District as District {
    externalizeWith {
        String pt.ist.standards.geographic.GeographicInfoLoader.externalizePlace();
    }

    internalizeWith pt.ist.standards.geographic.GeographicInfoLoader.internalizePlace();
}

valueType pt.ist.standards.geographic.Country as Country {
    externalizeWith {
        String pt.ist.standards.geographic.GeographicInfoLoader.externalizePlace();
    }

    internalizeWith pt.ist.standards.geographic.GeographicInfoLoader.internalizePlace();
}

/************
 * ENTITIES *
 ************/

/*-------------------
 * REFERENCE ENTITIES
 * ------------------*/

class FiscalCountryRegion {
	String fiscalCode;
	LocalizedString name;
}

class CustomerType {
	String code (REQUIRED);
	LocalizedString name (REQUIRED);
}

class VatType {
	String code;
	LocalizedString name;
}

class Vat {
	BigDecimalT taxRate;
	DateTime beginDate;
	DateTime endDate;
}

class VatExemptionReason
{
	String code;
	LocalizedString name;
}

class ProductGroup {
	String code;
	LocalizedString name;
}

class Product {
	String code;
	LocalizedString name;
	LocalizedString unitOfMeasure;
	boolean active;
}

class Currency {
	String code;
	LocalizedString name;
	String isoCode;
	String symbol;
}

class TreasuryExemption {
	String code;
	LocalizedString name;
	BigDecimalT discountRate;
}

class PaymentMethod {
	String code;
	LocalizedString name;
}

/*-----------
 * Finantial 
 * ----------*/
 
class FinantialInstitution {
	String code (REQUIRED);
	String fiscalNumber (REQUIRED);
	String companyId;
	String name (REQUIRED);
	String companyName;
	String address;
	Country country;
	District district;
	Municipality municipality;
	String locality;
	String zipCode;
}

class FinantialEntity {
	String code (REQUIRED);
	LocalizedString name (REQUIRED);
} 

class Customer {
}

class AdhocCustomer extends Customer {
	String code;
	String fiscalNumber;
	String identificationNumber;
	String name;
	String address;
	String districtSubdivision;
	String zipCode;
	String countryCode;
}

class debt.DebtAccount {
	boolean closed;
}

class document.TreasuryDocumentTemplate {
}

class document.TreasuryDocumentTemplateFile extends .org.fenixedu.bennu.io.domain.GenericFile {
    boolean active;
}

/*-----------
 * Documents 
 * ----------*/

class document.FinantialDocumentType {
	FinantialDocumentTypeEnum type;
	String code;
	LocalizedString name;
	String documentNumberSeriesPrefix;
	boolean invoice;
	
}

class document.Series {
	String code (REQUIRED);
	LocalizedString name (REQUIRED);
	boolean externSeries;
	boolean certificated;
	boolean legacy;
	boolean defaultSeries;
}

class document.DocumentNumberSeries {
	int counter;
}

class event.TreasuryEvent {
	LocalizedString description;
	String propertiesJsonMap;
}

class document.FinantialDocument {
	String documentNumber;
	DateTime documentDate;
	DateTime documentDueDate;
	String originDocumentNumber;
	FinantialDocumentStateType state;
}


	class document.Invoice extends document.FinantialDocument {
	}
	
		class document.DebitNote extends document.Invoice {
		}
		
		class document.CreditNote extends document.Invoice {
		}
	
	class document.SettlementNote extends document.FinantialDocument {
	}

class document.FinantialDocumentEntry {
	FinantialEntryType finantialEntryType;
	String description;
	BigDecimalT amount;
	DateTime entryDateTime;
}

	class document.InvoiceEntry extends document.FinantialDocumentEntry {
	BigDecimalT quantity;
	}

		class document.DebitEntry extends document.InvoiceEntry {
			boolean eventAnnuled;
			LocalDate dueDate;
			String propertiesJsonMap;
		}
		
		class document.CreditEntry extends document.InvoiceEntry {
		}

	class document.SettlementEntry extends document.FinantialDocumentEntry {
	}

class document.PaymentEntry {
	BigDecimalT payedAmount;
}

class document.ReimbursementEntry {
	BigDecimalT reimbursedAmount;
}


/*
class debt.DebitEntrySuspensionRequest {
}

class debt.DebitEntrySuspensionRequestEntry {
}
*/

/*-----------
 * Tariff 
 * ----------*/

class tariff.Tariff {
	DateTime beginDate;
	DateTime endDate;
	DueDateCalculationType dueDateCalculationType;
	LocalDate fixedDueDate;
	int numberOfDaysAfterCreationForDueDate;
	boolean applyInterests;
}

	class tariff.FixedTariff extends tariff.Tariff {
		BigDecimalT amount;
	}
	
class tariff.InterestRate {
	InterestType interestType;
	int numberOfDaysAfterDueDate;
	boolean applyInFirstWorkday;
	int maximumDaysToApplyPenalty;
	int maximumMonthsToApplyPenalty;
	BigDecimalT interestFixedAmount;
	BigDecimalT rate;
}

class tariff.GlobalInterestRate {
	int year;
	LocalizedString description;
	BigDecimalT rate;
}

/* ----
 * SIBS
 * ----
 */

class paymentcodes.PaymentReferenceCode
{
       String referenceCode;
       DateTime beginDate;
       DateTime endDate;
       PaymentReferenceCodeStateType state;
}

	class paymentcodes.pool.PaymentCodePool  {
	  String name;
	  Integer minPaymentCodes;
	  Integer maxPaymentCodes;
	  BigDecimalT minAmount;
	  BigDecimalT maxAmount;
	  Boolean active;
	}
	class paymentcodes.SibsReportFile extends .org.fenixedu.bennu.io.domain.GenericFile {
	  LocalDate whenProcessedBySibs;
	  BigDecimalT transactionsTotalAmount;
	  BigDecimalT totalCost;
	}
	
	class paymentcodes.SibsInputFile extends .org.fenixedu.bennu.io.domain.GenericFile {
	}

class paymentcodes.PaymentReferenceCodeManager
{
       String code;
       String description;
}

class integration.Configuration
{
       String code;
       String externalURL;
       String username;
       String password;
}

class integration.OperationFile extends .org.fenixedu.bennu.io.domain.GenericFile 
{
}

class integration.IntegrationOperation
{
       DateTime executionDate;
       boolean processed;
       boolean success;
       String errorLog;
}

class integration.ExportOperation extends integration.IntegrationOperation
{
}

class integration.ImportOperation extends integration.IntegrationOperation
{
}

/*------------------
 * TREASURY SETTINGS
 * -----------------*/

class settings.TreasurySettings {
}

relation TreasurySettingsBennu {
	.org.fenixedu.bennu.core.domain.Bennu playsRole bennu {
		multiplicity 1..1;
	}
	
	settings.TreasurySettings playsRole treasurySettings {
		multiplicity *;
	}
}

relation TreasurySettingsCurrency {
	
	settings.TreasurySettings playsRole treasurySettings {
		multiplicity 0..1;
	}
	
	Currency playsRole defaultCurrency {
		multiplicity 0..1;
	}
}

relation TreasurySettingsVatType {

	settings.TreasurySettings playsRole treasurySettings {
		multiplicity 0..1;
	}
	
	VatType playsRole defaultVatType {
		multiplicity 0..1;
	}
}

/*************
 * RELATIONS *
 *************/

/*-------------------
 * REFERENCE ENTITIES
 * ------------------*/

relation FinantialInstitutionFiscalCountryRegion{
	FiscalCountryRegion playsRole fiscalCountryRegion {
		multiplicity 1..1;
	}
	
	FinantialInstitution playsRole finantialInstitutions {
		multiplicity *;
	}
}

relation FinantialInstitutionCurrency{
	Currency playsRole currency {
		multiplicity 1..1;
	}
	
	FinantialInstitution playsRole finantialInstitutions {
		multiplicity *;
	}
}

relation FiscalCountryRegionBennu {
	.org.fenixedu.bennu.core.domain.Bennu playsRole bennu {
		multiplicity 1..1;
	}
	
	FiscalCountryRegion playsRole fiscalCountryRegions {
		multiplicity *;
	}
}

relation CustomerTypeBennu {
	.org.fenixedu.bennu.core.domain.Bennu playsRole bennu {
		multiplicity 1..1;
	}
	
	CustomerType playsRole customerTypes {
		multiplicity *;
	}
}

relation VatTypeBennu {
	.org.fenixedu.bennu.core.domain.Bennu playsRole bennu {
		multiplicity 1..1;
	}
	
	VatType playsRole vatTypes {
		multiplicity *;
	}
}

relation VatBennu {
	.org.fenixedu.bennu.core.domain.Bennu playsRole bennu {
		multiplicity 1..1;
	}
	
	Vat playsRole vats {
		multiplicity *;
	}
}

relation VatVatType {
	VatType playsRole vatType {
		multiplicity 1..1;
	}
	
	Vat playsRole vats {
		multiplicity *;
	}
}

relation VatFinantialInstitution {
	FinantialInstitution playsRole finantialInstitution {
		multiplicity 1..1;
	}
	
	Vat playsRole vats {
		multiplicity *;
	}
}

relation VatExemptionReasonBennu {
	.org.fenixedu.bennu.core.domain.Bennu playsRole bennu {
		multiplicity 1..1;
	}
	
	VatExemptionReason playsRole vatExemptionReasons {
		multiplicity *;
	}
}

relation VatVatExemptionReason {
	VatExemptionReason playsRole vatExemptionReason {
		multiplicity 0..1;
	}
	
	Vat playsRole vats {
		multiplicity *;
	}
}


relation ProductGroupBennu {
	.org.fenixedu.bennu.core.domain.Bennu playsRole bennu {
		multiplicity 1..1;
	}
	
	ProductGroup playsRole productGroups {
		multiplicity *;
	}
}

relation ProductBennu {
	.org.fenixedu.bennu.core.domain.Bennu playsRole bennu {
		multiplicity 1..1;
	}
	
	Product playsRole products {
		multiplicity *;
	}
}

relation ProductProductGroup {
	ProductGroup playsRole productGroup {
		multiplicity 1..1;
	}
	
	Product playsRole products {
		multiplicity *;
	}
}

relation CurrencyBennu {
	.org.fenixedu.bennu.core.domain.Bennu playsRole bennu {
		multiplicity 1..1;
	}
	
	Currency playsRole currencies {
		multiplicity *;
	}
}

relation TreasuryExemptionBennu {
	.org.fenixedu.bennu.core.domain.Bennu playsRole bennu {
		multiplicity 1..1;
	}
	
	TreasuryExemption playsRole treasuryExemptions {
		multiplicity *;
	}	
}

/*
relation TreasuryExemptionDebitEntry
{
	document.DebitEntry playsRole debitEntry{
		multiplicity 0..1;
	}
	
	TreasuryExemption playsRole treasuryExemptions{
		multiplicity 1..1;
	}
}
*/

relation PaymentMethodBennu {
	.org.fenixedu.bennu.core.domain.Bennu playsRole bennu {
		multiplicity 1..1;
	}
	
	PaymentMethod playsRole paymentMethods {
		multiplicity *;
	}
}

/*-----------
 * Finantial 
 * ----------*/

relation FinantialInstitutionBennu {
	.org.fenixedu.bennu.core.domain.Bennu playsRole bennu {
		multiplicity 1..1;
	}
	
	FinantialInstitution playsRole finantialInstitutions {
		multiplicity *;
	}
}

relation ProductFinantialInstitution {
	FinantialInstitution playsRole finantialInstitutions {
		multiplicity *;
	}
	
	Product playsRole availableProducts {
		multiplicity *;
	}
}

relation FinantialEntityBennu {
	.org.fenixedu.bennu.core.domain.Bennu playsRole bennu {
		multiplicity 1..1;
	}
	
	FinantialEntity playsRole finantialEntities {
		multiplicity *;
	}	
}

relation FinantialInstitutionFinantialEntity {
	FinantialInstitution playsRole finantialInstitution {
		multiplicity 1..1;
	}
	
	FinantialEntity playsRole finantialEntities {
		multiplicity *;
	}
}

relation CustomerBennu {
	.org.fenixedu.bennu.core.domain.Bennu playsRole bennu {
		multiplicity 1..1;
	}
	
	Customer playsRole customers {
		multiplicity *;
	}
}

relation DebtAccountBennu {
	.org.fenixedu.bennu.core.domain.Bennu playsRole bennu {
		multiplicity 1..1;
	}
	
	debt.DebtAccount playsRole debtAccounts {
		multiplicity *;
	}
}

relation DebtAccountCustomer {
	Customer playsRole customer {
		multiplicity 1..1;
	}
	
	debt.DebtAccount playsRole debtAccounts {
		multiplicity *;
	}
}

relation DebtAccountFinantialInstitution {
	FinantialInstitution playsRole finantialInstitution {
		multiplicity 1..1;
	}
	
	debt.DebtAccount playsRole debtAccounts {
		multiplicity *;
	}
}

/* --- Document Template --- */

relation TreasuryDocumentTemplateBennu {
    .org.fenixedu.bennu.core.domain.Bennu playsRole bennu {
        multiplicity 1..1;
    }
    
    document.TreasuryDocumentTemplate playsRole treasuryDocumentTemplates {
        multiplicity *;
    }
}

relation TreasuryDocumentTemplateFinantialDocumentType {
    document.FinantialDocumentType playsRole finantialDocumentType {
        multiplicity 1..1;
    }

    document.TreasuryDocumentTemplate playsRole treasuryDocumentTemplates {
        multiplicity *;
    }
}

relation TreasuryDocumentTemplateFinantialEntity {
    FinantialEntity playsRole finantialEntity {
        multiplicity 1..1;
    }
    
    document.TreasuryDocumentTemplate playsRole treasuryDocumentTemplates {
        multiplicity *;
    }
}

relation TreasuryDocumentTemplateDocumentTemplateFile {
    document.TreasuryDocumentTemplate playsRole treasuryDocumentTemplate {
        multiplicity 1..1;
    }
    
    document.TreasuryDocumentTemplateFile playsRole treasuryDocumentTemplateFiles {
        multiplicity *;
    }
}

/* --- Document Template File --- */

relation TreasuryDocumentTemplateFileBennu {
    .org.fenixedu.bennu.core.domain.Bennu playsRole bennu {
        multiplicity 1..1;
    }
    
    document.TreasuryDocumentTemplateFile playsRole treasuryDocumentTemplateFiles {
        multiplicity *;
    }
}

/*-----------
 * Documents 
 * ----------*/

/* --- FinantialDocumentType --- */

relation FinantialDocumentTypeBennu {
	.org.fenixedu.bennu.core.domain.Bennu playsRole bennu {
		multiplicity 1..1;
	}
	
	document.FinantialDocumentType playsRole finantialDocumentTypes {
		multiplicity *;
	}
}

/* --- Series --- */

relation SeriesBennu {
	.org.fenixedu.bennu.core.domain.Bennu playsRole bennu {
		multiplicity 1..1;
	}
	
	document.Series playsRole series {
		multiplicity *;
	}
}

relation SeriesFinantialInstitution {
	FinantialInstitution playsRole finantialInstitution {
		multiplicity 1..1;
	}
	
	document.Series playsRole series {
		multiplicity *;
	}
}

/* --- DocumentNumberSeries --- */

relation DocumentNumberSeriesBennu {
	.org.fenixedu.bennu.core.domain.Bennu playsRole bennu {
		multiplicity 1..1;
	}
	
	document.DocumentNumberSeries playsRole documentNumberSeries {
		multiplicity *;
	}
}

relation DocumentNumberSeriesSeries {
	document.Series playsRole series {
		multiplicity 1..1;
	}

	document.DocumentNumberSeries playsRole documentNumberSeries {
		multiplicity *;
	}
}

relation DocumentNumberSeriesFinantialDocumentType {
	document.FinantialDocumentType playsRole finantialDocumentType {
		multiplicity 1..1;
	}
	
	document.DocumentNumberSeries playsRole documentNumberSeries {
		multiplicity *;
	}
}

/* --- TreasuryEvent --- */

relation TreasuryEventBennu {
	.org.fenixedu.bennu.core.domain.Bennu playsRole bennu {
		multiplicity 1..1;
	}

	event.TreasuryEvent playsRole treasuryEvents {
		multiplicity *;
	}
}

relation TreasuryEventProduct {
	Product playsRole product {
		multiplicity 1..1;
	}
	
	event.TreasuryEvent playsRole treasuryEvents {
		multiplicity *;
	}
}

/* --- FinantialDocument --- */

relation FinantialDocumentBennu {
	.org.fenixedu.bennu.core.domain.Bennu playsRole bennu {
		multiplicity 1..1;
	}

	document.FinantialDocument playsRole finantialDocuments {
		multiplicity *;
	}
}

relation FinantialDocumentFinantialDocumentType {
	document.FinantialDocumentType playsRole finantialDocumentType {
		multiplicity 1..1;
	}
	
	document.FinantialDocument playsRole finantialDocuments {
		multiplicity *;
	}
}

relation FinantialDocumentDocumentNumberSeries {
	document.DocumentNumberSeries playsRole documentNumberSeries {
		multiplicity 1..1;
	}
	
	document.FinantialDocument playsRole finantialDocuments {
		multiplicity *;
	}
}

relation FinantialDocumentCurrency {
	Currency playsRole currency {
		multiplicity 1..1;
	}
	
	document.FinantialDocument playsRole finantialDocuments {
		multiplicity *;
	}
}

relation FinantialDocumentDebtAccount {
	debt.DebtAccount playsRole debtAccount {
		multiplicity 1..1;
	}
	
	document.FinantialDocument playsRole finantialDocuments {
		multiplicity *;
	}
}

/* --- Invoice --- */

relation InvoiceDebtAccount {
	debt.DebtAccount playsRole payorDebtAccount {
		multiplicity 0..1;
	}
	
	document.Invoice playsRole invoice {
		multiplicity *;
	}
}

/* --- CreditNote --- */

relation CreditNoteDebitNote {
	document.DebitNote playsRole debitNote {
		multiplicity 1..1;
	}
	
	document.CreditNote playsRole creditNote {
		multiplicity *;
	}
}

/* --- FinantialDocumentEntry --- */

relation FinantialDocumentEntryBennu {
	.org.fenixedu.bennu.core.domain.Bennu playsRole bennu {
		multiplicity 1..1;
	}
	
	document.FinantialDocumentEntry playsRole finantialDocumentEntries {
		multiplicity *;
	}
}

relation FinantialDocumentEntryFinantialDocument {
	document.FinantialDocument playsRole finantialDocument {
		multiplicity 0..1;
	}
	
	document.FinantialDocumentEntry playsRole finantialDocumentEntries {
		multiplicity *;
	}
}

/* --- InvoiceEntry --- */

relation InvoiceEntryDebtAccount {
	debt.DebtAccount playsRole debtAccount {
		multiplicity 1..1;
	}
	
	document.InvoiceEntry playsRole invoiceEntry {
		multiplicity *;
	}
}

relation InvoiceEntryCurrency {
	Currency playsRole currency {
		multiplicity 1..1;
	}
	
	document.InvoiceEntry playsRole invoiceEntry {
		multiplicity *;
	}
}

relation InvoiceEntryProduct {
	Product playsRole product {
		multiplicity 1..1;
	}
	
	document.InvoiceEntry playsRole invoiceEntries {
		multiplicity *;
	}
}

relation InvoiceEntryVat {
	Vat playsRole vat {
		multiplicity 1..1;
	}
	
	document.InvoiceEntry playsRole invoiceEntries {
		multiplicity *;
	}
}

/* --- DebitEntry --- */

relation DebitEntryTreasuryEvent {
	event.TreasuryEvent playsRole treasuryEvent {
		multiplicity 0..1;
	}
		
	document.DebitEntry playsRole debitEntries {
		multiplicity *;
	}
}

relation DebitEntryTariff {
    document.DebitEntry playsRole debitEntry {
        multiplicity *;
    }
    
    tariff.Tariff playsRole tariff {
        multiplicity 1..1;
    }
}

relation DebitEntryInterestDebitEntry {
    document.DebitEntry playsRole debitEntry {
        multiplicity 0..1;
    }
    
    document.DebitEntry playsRole interestDebitEntries {
        multiplicity *;
    }
}

/* --- CreditEntry --- */

relation CreditEntryDebitEntry {
	document.DebitEntry playsRole debitEntry {
		multiplicity 1..1;
	}

	document.CreditEntry playsRole creditEntries {
		multiplicity *;
	}
}


/* --- SettlementEntry --- */

relation SettlementEntryInvoiceEntry {
	document.InvoiceEntry playsRole invoiceEntry {
		multiplicity 1..1;
	}
	
	document.SettlementEntry playsRole settlementEntries {
		multiplicity *;
	}
}

/* --- PaymentEntry --- */

relation PaymentEntryBennu {
	.org.fenixedu.bennu.core.domain.Bennu playsRole bennu {
		multiplicity 1..1;
	}
	
	document.PaymentEntry playsRole paymentEntries {
		multiplicity *;
	}
}

relation PaymentEntrySettlementNote {
	document.SettlementNote playsRole settlementNote {
		multiplicity 1..1;
	}

	document.PaymentEntry playsRole paymentEntries {
		multiplicity *;
	}
}

relation PaymentEntryPaymentMode {
	PaymentMethod playsRole paymentMethod {
		multiplicity 1..1;
	}
	
	document.PaymentEntry playsRole paymentEntries {
		multiplicity *;
	}
}


/* --- ReimbursementEntry --- */

relation ReimbursementEntryBennu {
	.org.fenixedu.bennu.core.domain.Bennu playsRole bennu {
		multiplicity 1..1;
	}
	
	document.ReimbursementEntry playsRole reimbursementEntries {
		multiplicity *;
	}
}

relation ReimbursementEntrySettlementNote {
	document.SettlementNote playsRole settlementNote {
		multiplicity 1..1;
	}

	document.ReimbursementEntry playsRole reimbursementEntries {
		multiplicity *;
	}
}

/* SOMETHING IS MISSING HERE
relation FinantialDocumentPendingForExportationBennu {
	document.FinantialDocument playsRole finantialDocumentsPendingForExportation {
		multiplicity *;
	}
}
*/

/* --- Tariff --- */

relation TariffBennu {
	.org.fenixedu.bennu.core.domain.Bennu playsRole bennu {
		multiplicity 1..1;
	}
	
	tariff.Tariff playsRole tariffs {
		multiplicity *;
	}
}

relation TariffProduct {
	Product playsRole product {
		multiplicity 1..1;
	}
	
	tariff.Tariff playsRole tariff {
		multiplicity *;
	}
}

relation ProductVatType {
	VatType playsRole vatType {
		multiplicity 0..1;
	}
	
	Product playsRole products{
		multiplicity *;
	}
}

relation TariffFinantialEntity {
	.org.fenixedu.treasury.domain.FinantialEntity playsRole finantialEntity {
		multiplicity 1..1;
	}
	
	tariff.Tariff playsRole tariff {
		multiplicity *;
	}
}

/* --- InterestRate --- */

relation InterestRateBennu {
	.org.fenixedu.bennu.core.domain.Bennu playsRole bennu {
		multiplicity 1..1;
	}
	
	tariff.InterestRate playsRole interestRates {
		multiplicity *;
	}
}

relation InterestRateTariff {
	tariff.Tariff playsRole tariff {
		multiplicity 1..1;
	}
	
	tariff.InterestRate playsRole InterestRate {
		multiplicity 0..1;
	}
}

/* --- IntegrationOperation --- */

relation IntegrationOperationBennu
{
	.org.fenixedu.bennu.core.domain.Bennu playsRole bennu {
		multiplicity 1..1;
	}
	
	integration.IntegrationOperation playsRole integrationOperations {
		multiplicity *;
	}
}

relation IntegrationOperationFinantialInstitution
{
	FinantialInstitution playsRole finantialInstitution {
		multiplicity 1..1;
	}
	
	integration.IntegrationOperation playsRole integrationOperations {
		multiplicity *;
	}
}

relation IntegrationOperationFinantialDocuments
{
    document.FinantialDocument playsRole documents {
		multiplicity *;
	}
	
	integration.IntegrationOperation playsRole integrationOperations {
		multiplicity *;
	}
}


relation IntegrationOperationOperationFile
{
	integration.OperationFile playsRole file {
		multiplicity 1..1;
	}
	
	integration.IntegrationOperation playsRole integrationOperation
	{
		multiplicity 1..1;
	}
}


relation PaymentReferenceCodeManagerBennu {
	  .org.fenixedu.bennu.core.domain.Bennu  playsRole bennu {
	    multiplicity 1..1;
	  }
	  
	  paymentcodes.PaymentReferenceCodeManager playsRole paymentReferenceCodeManagers{
	    multiplicity *;
	  }
	}

relation PaymentReferenceCodeBennu {
	  .org.fenixedu.bennu.core.domain.Bennu  playsRole bennu {
	    multiplicity 1..1;
	  }
	  
	  paymentcodes.PaymentReferenceCode playsRole paymentReferenceCodes {
	    multiplicity *;
	  }
	}


relation SibsReportFileBennu {
	  .org.fenixedu.bennu.core.domain.Bennu  playsRole bennu {
	    multiplicity 1..1;
	  }
	  
	  paymentcodes.SibsReportFile playsRole sibsReportFiles {
	    multiplicity *;
	  }
	}
	
	
	relation SibsInputFileBennu {
	 paymentcodes.SibsInputFile playsRole sibsInputFiles {
	    multiplicity *;
	  }
	  
	  .org.fenixedu.bennu.core.domain.Bennu  playsRole bennu {
	    multiplicity 1..1;
	  }
	}
	
	relation SibsInputFileUser {
	  paymentcodes.SibsInputFile playsRole sibsInputFiles {
	    multiplicity *;
	  }
	  
	  .org.fenixedu.bennu.core.domain.User playsRole uploader {
	    multiplicity 1..1;
	  }
	  
	}
	
